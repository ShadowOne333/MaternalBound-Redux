//------------------------------------
// 	Key Items menu
//------------------------------------
// A hijack of the original "Check" menu option to instead work as a Key items menu
// This frees up space in the main inventory for the player

//------------------------------------
// 	File imports
//------------------------------------
import "../essential/asm65816.ccs"
import "../essential/cc_asmcall.ccs"
import "../essential/ccexpand.ccs"
import "../essential/ccscript_missing_commands.ccs"
import "../essential/commands.ccs"
import "../essential/flags.ccs"
import "../redux/window_titles.ccs"

//------------------------------------
// 	Definitions
//------------------------------------

// Define routines
define UNFREEZE_OBJECTS		= 0xC09451
define PLAY_SOUND		= 0xC0ABE0

define CLOSE_HPPP_WINDOWS	= 0xC10000
define SET_WINDOW_FOCUS		= 0xC1007E
define CLOSE_FOCUS_WINDOW	= 0xC10084
define CLOSEALL_WINDOWS		= 0xC1008E
define CREATE_WINDOW		= 0xC104EE
define HIDE_HPPP_WINDOWS	= 0xC10A1D
define PRINT_STRING		= 0xC10EFC
define WINDOW_TICK		= 0xC12DD5
define MAIN_PAUSE_MENU		= 0xC134CD
define ENTITY_FADE_ENTITY	= 0xC1B4A8
define CLOSE_INVENTORY_WHICH_WINDOWS	= 0xC135DE
define CLOSE_INVENTORY_MENU	= 0xC13648
define DISPLAY_TEXT		= 0xC186B1
define OPEN_WHICH_WINDOW	= 0xC193E7
define CLOSE_WHICH_WINDOW	= 0xC19437
define CREATE_WINDOW_REDIRECT	= 0xC1DD47

define SET_WINDOW_TITLE		= 0xC2032B
define GET_EVENT_FLAG		= 0xC21628

define CLEAR_INSTANT_PRINTING	= 0xC3E4CA
define SET_INSTANT_PRINTING	= 0xC3E4D4
define CLOSE_WINDOW		= 0xC3E521

// Define window constants
define MAINMENU_WINDOW	= 0
define DIALOGUE_WINDOW	= 1
define INVENTORY_WINDOW	= 2
define CASH_WINDOW	= 10
define WHICH_WINDOW	= 40
define USE_HELP_WINDOW	= 61

// Definitions for each Key item:
define ATM_CARD		= 177	// 0xB1
define SOUND_STONE	= 196	// 0xC4
define TOWN_MAP		= 202	// 0xCA
define SHACK_KEY	= 170	// 0xAA
define BICYCLE		= 176	// 0xB0
define RECEIVER_PHONE	= 181	// 0xB5
define PENCIL_ERASER	= 184	// 0xB8
define ERASER_ERASER	= 210	// 0xD2
define CABIN_KEY	= 171	// 0xAB
define WAD_BILLS	= 180	// 0xB4
define BACKSTAGE_PASS	= 125	// 0x7D
define SHOW_TICKET	= 178	// 0xB2
define ZOMBIE_PAPER	= 174	// 0xAE
define DIAMOND		= 182	// 0xB6
define YOGURT_DISP	= 139	// 0x8B
define SIGNED_BANANA	= 183	// 0xB7
define HIEROGLYPH	= 185	// 0xB9
define CARROT_KEY	= 253	// 0xFD
define TOWER_KEY	= 192	// 0xC0
define HAWK_EYE		= 175	// 0xAF
define SHYNESS_BOOK	= 164	// 0xA4
define TENDAKRAUT	= 211	// 0xD3
define METEORITE	= 193	// 0xC1
define LOCKER_KEY	= 205	// 0xCD
define BAD_KEY_MACHINE	= 172	// 0xAC
define BUBBLE_GUM	= 104	// 0x68

//------------------------------------
// 	Check code hijack
//------------------------------------
// Hijack the 'Check' routine
ROM[0xC13BCF] = {
	MText	(Key_Items_Prep)

	LDA_i	(USED_ITEM_TEMP_FLAG)
	JSL	(GET_EVENT_FLAG)
	CMP_i	(0x0000)
	BNE	(0x07)	// _exit_on_item_use
	JSL	(CC_Close_Custom_Windows)	// Close our custom windows
	JMP	(MAIN_PAUSE_MENU)	// Return to the Main menu

_exit_on_item_use:
	JMP	(0xC13C16)
	// Limit for this section is up to 0xC13C01
}

CC_Close_Custom_Windows: {
	// Close all windows properly through ASM
	LDA_i	(DIALOGUE_WINDOW)	// Load standard dialogue window
	JSL	(CLOSE_WINDOW)		// Close window routine

	LDA_i	(USE_HELP_WINDOW)	// Load inventory menu (Use/Help) window
	JSL	(CLOSE_WINDOW)		// Close window routine

	LDA_i	(WHICH_WINDOW)		// Load "Which?" text window
	JSL	(CLOSE_WINDOW)		// Close window routine

	LDA_i	(INVENTORY_WINDOW)	// Load inventory window
	JSL	(CLOSE_WINDOW)		// Close window routine

	LDA_i	(SND_SHOOT)		// 0x1B, 27, #SFX::MENU_OPEN_CLOSE
	JSL	(PLAY_SOUND)

	RTL
}

Key_Items_Prep: {
	call(open_which_window)
	open_hp
	open_wallet
	goto(KeyItems)
}

//------------------------------------
// 	Item counters
//------------------------------------
SetCount1: { counter(1) ctoarg eob }
SetCount2: { counter(2) ctoarg eob }
SetCount3: { counter(3) ctoarg eob }
SetCount4: { counter(4) ctoarg eob }
SetCount5: { counter(5) ctoarg eob }
SetCount6: { counter(6) ctoarg eob }
SetCount7: { counter(7) ctoarg eob }
SetCount8: { counter(8) ctoarg eob }
SetCount9: { counter(9) ctoarg eob }
SetCount10: { counter(10) ctoarg eob }
SetCount11: { counter(11) ctoarg eob }
SetCount12: { counter(12) ctoarg eob }
SetCount13: { counter(13) ctoarg eob }
SetCount14: { counter(14) ctoarg eob }
SetCount15: { counter(15) ctoarg eob }
SetCount16: { counter(16) ctoarg eob }
SetCount17: { counter(17) ctoarg eob }
SetCount18: { counter(18) ctoarg eob }
SetCount19: { counter(19) ctoarg eob }
SetCount20: { counter(20) ctoarg eob }
SetCount21: { counter(21) ctoarg eob }
SetCount22: { counter(22) ctoarg eob }
SetCount23: { counter(23) ctoarg eob }
SetCount24: { counter(24) ctoarg eob }
SetCount25: { counter(25) ctoarg eob }
SetCount26: { counter(26) ctoarg eob }


//------------------------------------
//   Main Key items ccscript code
//------------------------------------
KeyItems:
_KeyItemsReturn:
	unset(USED_ITEM_TEMP_FLAG)

	copy_wram
	save_text_window_state
	window_open(INVENTORY_WINDOW)
	title(0, KeyItems_Title)
	copy_active
	counter_zero
	ctoarg

	// Item flag checks
	// Check if the player is not in Jeff nor Poo's sections
	// (Main gameplay with all characters)
	if not flag flg_PooSoloMission and not flag flg_JeffSoloMission {

		add_menu_option_with_callback("ATM card", SetCount1)
		if flag FLG_ONET_2FPICKEY_APPEAR and not flag FLG_WIN_OSCAR { // Buzz Buzz died and Defeated Nightmare
		// Flag FLG_ONET_2FPICKEY_APPEAR is set after Buzz buzz is killed, but never unset
			add_menu_option_with_callback("Sound stone", SetCount2) }
		if flag FLG_ITEM_MAP {
			add_menu_option_with_callback("Town map", SetCount3) }
		if flag FLG_KEY_TABIGOYA and not flag FLG_OPEN_TABIGOYA	{ // Shack Key used
			add_menu_option_with_callback("Key to the shack", SetCount4) }
		if flag FLG_ITEM_CYCLE {
			add_menu_option_with_callback("Bicycle", SetCount5) }
		if flag FLG_ITEM_PHONE {
			add_menu_option_with_callback("Receiver phone", SetCount6) }
		if flag FLG_ITEM_TACO_ERASER {
			add_menu_option_with_callback("Pencil eraser", SetCount7) }
		if flag FLG_ITEM_KOKESHI {
			add_menu_option_with_callback("Eraser eraser", SetCount8) }
		if flag FLG_HAPPY_CARPAINTER_ITEMFULL and not flag FLG_POLA_GRFD { // Paula rescued
			add_menu_option_with_callback("Key to the cabin", SetCount9) }
		if flag FLG_TWSN_ITEM_TONCHIKI and not flag FLG_TWSN_TONZURABUS_APPEAR {
			add_menu_option_with_callback("Suitcase of bills", SetCount10) }
		if flag flg_KeyItem_BackstagePass {
			add_menu_option_with_callback("Backstage pass", SetCount11) }
		if flag FLG_ITEM_HOIHOI and not flag FLG_PUT_ZOMBI_HOIHOI and not flag FLG_THRK_ZOMBI_CAPTURED and not flag FLG_WIN_GEPPU {
			add_menu_option_with_callback("Zombie paper", SetCount12) }
		if flag flg_KeyItem_ShowTicket {
			add_menu_option_with_callback("Show ticket", SetCount13) }
		if flag FLG_DSRT_ITEM_DIA and not flag FLG_FOUR_MISSFAKE {
			add_menu_option_with_callback("Diamond", SetCount14) }
		if flag flg_KeyItem_YogurtMachine {	// Yogurt dispenser -> Tofu machine
			add_menu_option_with_callback("Tofu machine", SetCount15) }
		if flag FLG_ITEM_BANANA and not flag FLG_FOUR_KOMORITA_APPEAR {	// Open Sewer
			add_menu_option_with_callback("Signed banana", SetCount16) }
		if flag FLG_SUMS_HIEROGLYPH and not flag FLG_SPHINX_OK {	// Pyramid open
			add_menu_option_with_callback("Hieroglyph copy", SetCount17) }
		if flag flg_KeyItem_CarrotKey {	// Rabbit's carrot ->
			add_menu_option_with_callback("Rabbits' carrot", SetCount18) }
		if flag FLG_ITEM_KEY_PUPUKA and not flag FLG_SKRB_DUNGEONMAN_OPEN {
			add_menu_option_with_callback("Key to the tower", SetCount19) }
		if flag FLG_ITEM_TAKANOME and not flag FLG_MAKYO_USE_TAKANOME {
			add_menu_option_with_callback("Hawk eye", SetCount20) }
		if flag FLG_ITEM_MUKUCHI and not flag FLG_ONET_LUCY_CHU {	// Returned Shyness book
			add_menu_option_with_callback("Shyness book", SetCount21) }
		if flag FLG_GUMI_OLDMAN_ITEM and not flag FLG_DKFD_DOOR_DISAPPEAR {	// Open Underworld rock
			add_menu_option_with_callback("Tendakraut", SetCount22) }
		if flag FLG_ITEM_XYZ and not flag FLG_XYZ_OK {
			add_menu_option_with_callback("Meteorite piece", SetCount23) }
	}

	if not flag flg_PooSoloMission {

		if flag FLG_ITEM_KEY_LOCKER {
			add_menu_option_with_callback("Locker Key", SetCount24) }
		if flag FLG_ITEM_GAUS {
			add_menu_option_with_callback("Bad Key Machine", SetCount25) }
		if flag FLG_ITEM_BUBBLE_GUM {
			add_menu_option_with_callback("Bubble Gum Pak", SetCount26) }
	}

	print_vertical(2)
	if not create_menu_in_window_cancellable(INVENTORY_WINDOW) {
		// Exit
		// Closing the windows is handled through ASM after returning from the Key items menu
		eob
	}

	ctoarg
	swap

	switch_goto(26, {
		long atm
		long soundstone
		long townmap
		long shackkey
		long bike
		long phone
		long pencileraser
		long erasereraser
		long cabinkey
		long wadofbills
		long backstagepass
		long zombiepaper
		long showticket
		long diamond
		long yogurtdisp
		long signedbanana
		long hieroglyph
		long carrotkey
		long towerkey
		long hawkeye
		long shynessbook
		long tendakraut
		long meteoritepiece
		long lockerkey
		long badkeymachine
		long gum
	})

	eob

//------------------------------------
// 	  Item scripts
//------------------------------------
	atm:		use_help_submenu(ATM_CARD, data_04.l_0xc5566b) eob
	soundstone:	use_help_submenu(SOUND_STONE, data_04.l_0xc55ad5) eob
	townmap:	use_help_submenu(TOWN_MAP, data_04.l_0xc55cce) eob
	shackkey: 	use_help_submenu(SHACK_KEY, data_04.l_0xc554f6) eob
	bike:		use_help_submenu(BICYCLE, data_04.l_0xc55627) eob
	phone:		use_help_submenu(RECEIVER_PHONE, data_04.l_0xc5570b) eob
	pencileraser:	use_help_submenu(PENCIL_ERASER, data_04.l_0xc55794) eob
	erasereraser:	use_help_submenu(ERASER_ERASER, data_04.l_0xc55e28) eob
	cabinkey:	use_help_submenu(CABIN_KEY, data_04.l_0xc5551d) eob
	wadofbills:	use_help_submenu(WAD_BILLS, data_04.l_0xc556e3) eob
	backstagepass:	use_help_submenu(BACKSTAGE_PASS, data_03.l_0xc548b2) eob
	zombiepaper:	use_help_submenu(ZOMBIE_PAPER, data_04.l_0xc555b8) eob
	showticket:	use_help_submenu(SHOW_TICKET, data_04.l_0xc5569c) eob
	diamond:	use_help_submenu(DIAMOND, data_04.l_0xc55740) eob
	yogurtdisp:	use_help_submenu(YOGURT_DISP, data_04.l_0xc54c6d) eob	// Tofu machine
	signedbanana:	use_help_submenu(SIGNED_BANANA, data_04.l_0xc5576a) eob
	hieroglyph:	use_help_submenu(HIEROGLYPH, data_04.l_0xc557e7) eob
	carrotkey:	use_help_submenu(CARROT_KEY, data_05.l_0xc5689f) eob
	towerkey:	use_help_submenu(TOWER_KEY, data_04.l_0xc55943) eob
	hawkeye:	if not flag flg_InDeepDarkness { call(data_04.l_0xc55602) eob }	// Hawk eye description
			else { use_help_submenu(HAWK_EYE, data_04.l_0xc55602) eob }
	shynessbook:	use_help_submenu(SHYNESS_BOOK, data_04.l_0xc55409) eob
	tendakraut:	use_help_submenu(TENDAKRAUT, data_04.l_0xc55e77) eob
	meteoritepiece:	use_help_submenu(METEORITE, data_04.l_0xc55957) eob
	lockerkey:	use_help_submenu(LOCKER_KEY, data_04.l_0xc55d82) eob
	badkeymachine:	use_help_submenu(BAD_KEY_MACHINE, data_04.l_0xc55546) eob
	gum:		use_help_submenu(BUBBLE_GUM, data_03.l_0xc544dd) eob

// Opens up the small window with the "Which?" text
open_which_window: {
	cc_asmcall(SET_INSTANT_PRINTING, RET_NONE)
	window_open(WHICH_WINDOW)
	"Which?"
	cc_asmcall(CLEAR_INSTANT_PRINTING, RET_NONE)
	eob
}

//------------------------------------
//	Custom commands
//------------------------------------

// Define custom USE ITEM command
command use_item(itemID) {
	arg(itemID)
	cc_asmcall(CCUseItem, RET_NONE)
	eob
}

// Use/Help submenu command
command use_help_submenu( itemID, help_text ) {
	window_close(WHICH_WINDOW)
	window_open(USE_HELP_WINDOW)
	add_menu_string("Use")
	add_menu_string("Help!")
	print_vertical(1)
	create_menu_cancellable
	switch_goto(2, {
		long _use
		long _help
	})
	window_close(USE_HELP_WINDOW)
	call(open_which_window)
	window_switch(INVENTORY_WINDOW)
	goto(_KeyItemsReturn)
_use:
	window_closetop
	set(USED_ITEM_TEMP_FLAG)
	window_open(DIALOGUE_WINDOW)
	use_item(itemID)
	window_closeall
	eob
_help:
	window_switch(INVENTORY_WINDOW)
	window_clear
	window_open(DIALOGUE_WINDOW)
	call(help_text)
	window_close(DIALOGUE_WINDOW)
	window_close(USE_HELP_WINDOW)
	call(open_which_window)
	window_switch(INVENTORY_WINDOW)
	goto(_KeyItemsReturn)
}

// Set the flag for the corresponding Key Item
command give_menu_key_item(itemID) {
	counter(itemID)
	call(keyitemcounter)

	sound(SND_SPECIALITEM)
	"@(The {itemname(itemID)} was added to your Key Items.)" next
}

keyitemcounter:
	if counter_is(SOUND_STONE) { toggle_with_sound(FLG_BUNBUN) }
	if counter_is(SHACK_KEY) { toggle_with_sound(FLG_KEY_TABIGOYA) }
	if counter_is(BICYCLE) { toggle_with_sound(FLG_ITEM_CYCLE) }
	if counter_is(TOWN_MAP) { toggle_with_sound(FLG_ITEM_MAP) }
	if counter_is(RECEIVER_PHONE) { toggle_with_sound(FLG_ITEM_PHONE) }
	if counter_is(PENCIL_ERASER) { toggle_with_sound(FLG_ITEM_TACO_ERASER) }
	if counter_is(CABIN_KEY) { toggle_with_sound(FLG_HAPPY_CARPAINTER_ITEMFULL) }
	if counter_is(WAD_BILLS) { toggle_with_sound(FLG_TWSN_ITEM_TONCHIKI) }
	if counter_is(ZOMBIE_PAPER) { toggle_with_sound(FLG_ITEM_HOIHOI) }
	if counter_is(LOCKER_KEY) { toggle_with_sound(FLG_ITEM_KEY_LOCKER) }
	if counter_is(BAD_KEY_MACHINE) { toggle_with_sound(FLG_ITEM_GAUS) }
	if counter_is(ERASER_ERASER) { toggle_with_sound(FLG_ITEM_KOKESHI) }
	if counter_is(DIAMOND) { toggle_with_sound(FLG_DSRT_ITEM_DIA) }
	if counter_is(SIGNED_BANANA) { toggle_with_sound(FLG_ITEM_BANANA) }
	if counter_is(HIEROGLYPH) { toggle_with_sound(FLG_SUMS_HIEROGLYPH) }
	if counter_is(CARROT_KEY) { toggle_with_sound(flg_KeyItem_CarrotKey) }
	if counter_is(TOWER_KEY) { toggle_with_sound(FLG_ITEM_KEY_PUPUKA) }
	if counter_is(HAWK_EYE) { toggle_with_sound(FLG_ITEM_TAKANOME) }
	if counter_is(SHYNESS_BOOK) { toggle_with_sound(FLG_ITEM_MUKUCHI) }
	if counter_is(TENDAKRAUT) { toggle_with_sound(FLG_GUMI_OLDMAN_ITEM) }
	if counter_is(METEORITE) { toggle_with_sound(FLG_ITEM_XYZ) }
	if counter_is(BUBBLE_GUM) { toggle_with_sound(FLG_ITEM_BUBBLE_GUM) }
	if counter_is(BACKSTAGE_PASS) { toggle_with_sound(flg_KeyItem_BackstagePass) }
	if counter_is(YOGURT_DISP) { toggle_with_sound(flg_KeyItem_YogurtMachine) }
	if counter_is(SHOW_TICKET) { toggle_with_sound(flg_KeyItem_ShowTicket) }
eob

// Turn active flag off or vice versa
command toggle_with_sound(flg) {
	copy_wram
	toggle(flg)
	if flag flg { sound(SND_GIFTOPEN) }
	else { sound(SND_BOXTAKE) }
	copy_active
}
