import "../essential/asm65816.ccs"
import "../essential/cc_asmcall.ccs"
import "../essential/jsl_rts.ccs"
import "../redux/Bowspr.ccs"
import "../redux/movscr_codes.ccs"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Mother 3 Style Battle Sprites.
// This uses cooprocks123e's Battle Overworld Sprites.

// NOTES/LIMITATIONS:

// Overworld sprites used at the same time must share palette. Battles against four enemy types will always produce wrong
// colors in one of the battle sprites, there being two instances of this in the vanilla game (battles against the Loaded Dice).

// Use cc_asmcall(m3sbs_reset,RET_NONE) to update sprites/party member configuration mid-battle.
// Note that this resets Bowspr and could be not ideal.

// We use two bytes of free RAM to access the action scripts (it's just a lot easier this way).
define free_ram =	0x3260	// Since it's two bytes, 0x3251 would also be used, although it's always 0.

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

define mc1_sprite =	1	// Default sprite group to show for main character 1 (a.k.a. Ness).
define mc1_altsprite1 =	6	// Alternative sprites, these will show instead of the default one if the correct event flag is set.
define mc1_altsprite2 =	5
define mc1_altsprite3 =	1

define mc2_sprite =	2	// Default sprite group to show for main character 2 (a.k.a. Paula).
define mc2_altsprite1 =	343 //25
define mc2_altsprite2 =	2
define mc2_altsprite3 =	2

define mc3_sprite =	3	// Default sprite group to show for main character 3 (a.k.a. Jeff).
define mc3_altsprite1 =	355 // 25
define mc3_altsprite2 =	3
define mc3_altsprite3 =	3

define mc4_sprite =	4	// Default sprite group to show for main character 4 (a.k.a. Poo).
define mc4_altsprite1 =	457	// 25
define mc4_altsprite2 =	4
define mc4_altsprite3 =	4

// Flags: priority is flag 1, then flag 2, then flag 3.
// So, for example, if flag 1 is set, altsprite1 will be used, ignoring the states of flags 2 and 3.
// All three flags must be unset for the default sprite to be used.

define mc1_flag1 =	978	// Ness in Magicant (custom event flag, see code below).
define mc1_flag2 =	979	// Party in Cave of the Past (custom event flag, see code below).
define mc1_flag3 =	980

define mc2_flag1 =	979
define mc2_flag2 =	980
define mc2_flag3 =	980

define mc3_flag1 =	979
define mc3_flag2 =	980
define mc3_flag3 =	980

define mc4_flag1 =	979
define mc4_flag2 =	980
define mc4_flag3 =	980

// These ones control the y and x positions.
// They are adjusted for 16x24 sprites.
define y_pos =		168

define x0_pos =		45
define x1_pos =		78
define x2_pos =		101
define x3_pos =		133
define x4_pos =		157
define x5_pos =		189
define x6_pos =		213

// Default direction and animation frame to use for the sprites.
define direction =	4
define anim_frame =	0

// Direction and animation frame to use for the sprites in the "YOU WON" screen.
define yw_direction =	4
define yw_anim_frame =	0

// Use cc_asmcall(m3sbs_reset,RET_NONE) to update sprites/party member configuration mid-battle. Note that this resets Bowspr.

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Change the text to set/unset flags 978 and 979 / flags defined above, in Magicant and Cave of the Past for the change in sprites to apply.

define giygas_special_group = 478

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Fixes for special battles.

// Giygas change to phase 5. Wrong graphics.
ROM[0xC2C544] = JSL (giygas_fix_1)
// Prayer scenes.
ROM[0xC2C3A4] = JSL (giygas_fix_2)
// Giygas death scene.
ROM[0xC2C75B] = JSL (giygas_fix_3)

giygas_fix_1:
    JSL (0xC1DC1C)
    JSL (ASM_BowsprEnd)
    JSL (m3sbs_reset)
    RTL

giygas_fix_2:
    JSL (0xC1DD5F)
    PHD
    PHX
    PHA
    JSL (ASM_BowsprEnd)
    JSL (m3sbs_reset)
    PLA
    PLX
    PLD
    RTL

giygas_fix_3:
    JSL (0xC1DD41)
    PHD
    PHX
    PHA
    JSL (ASM_BowsprEnd)
    JSL (m3sbs_reset)
    PLA
    PLX
    PLD
    RTL

// Fix for the fight against Master Barf, when Poo comes back to the party.
ROM[0xC29302] = JSL (barf_fix_1)
ROM[0xC29388] = JSL (barf_fix_2)

barf_fix_1:
    PHD
    JSL (ASM_BowsprEnd)
    PLD
    JSL (0xC1DD41)
    RTL

barf_fix_2:
    JSL (0xC1DC1C)
    JSL (m3sbs_init)
    RTL

// Fixes related to temporal party members messing with Bowspr and crashing the game (teddy bears + flying men).
ROM[0xC229FF] = JSL (tpm_hijack_1)
ROM[0xC22907] = JSL (tpm_hijack_2)

tpm_hijack_1: {
    PHA
    LDA_a (0x9643)
    BEQ_a (_ow)
    LDA_i (16)
    STA_a (free_ram)
    LDA_i (8)
    JSL_RTS_C2 (0xC269BE)
    JSL (ASM_BowsprEnd)
    JSL (m3sbs_reset)
    PLA
    JSL (0xC03903)            // Original JSL.
    JSL (m3sbs_init)
    RTL
_ow:
    PLA
    JSL (0xC03903)
    RTL
}

tpm_hijack_2: {
    STA_d (0x02)
    STA_d (0x0E)
    JSL (ASM_BowsprEnd)
    JML (m3sbs_reset)
}

// Avoid updating teddy bears immediately if the item is received as a battle reward.
ROM[0xC18B80] = JML(special_fix_1)
special_fix_1: {
    LDA_a (0x9643)
    BNE( 4)
    JSL (0xC216DB)
    JML (0xC18B84)
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Touching stuff from this point on might cause unexpected results.

// Bowspr palette initialization bitmask (we only use palette 3, because the other ones are taken by the enemy sprites).
define bowspr_palette =        0x08

// Subroutine to reset m3sbs and recalculate sprites and positions to use (next time an HP window raises).
m3sbs_reset: {
    LDA_i (0)
    STA_a (free_ram)
    RTL
}

// Subroutine to lower everyone.
m3sbs_lower_all: {
    LDA_i (16)
    STA_a (free_ram)
    RTL
}

// RAM defines.
define RAM_SELECTED_CHARACTER =        0x89CA
define RAM_CONTROLLED_MEMBERS =        0x98A4
define RAM_OBJ_POS_Y =                0x0BCA
define RAM_OBJ_SCRIPT_VAR1 =        0x0E9A
define RAM_OBJ_SCRIPT_VAR2 =        0x0ED6
define RAM_OBJ_ANIMATION_FRAME =    0x10F2
define RAM_OBJ_MOVEMENT_DIRECTION =    0x2AF6
define RAM_BGMODE_MIRROR =            0x000F
// Battler struct.
define RAM_BS_CHAR_0 =                0x9FAC
define RAM_BS_CHAR_1 =                0x9FFA
define RAM_BS_CHAR_2 =                0xA048
define RAM_BS_CHAR_3 =                0xA096
define RAM_BS_STATUS_0 =            0x9FC9
define RAM_BS_STATUS_1 =            0xA017
define RAM_BS_STATUS_2 =            0xA065
define RAM_BS_STATUS_3 =            0xA0B3

// ROM address for JSR/RTL in bank $C2
define FREE_ROM_C2_S =                0xFFCC
define FREE_ROM_C2_L =                0xC2FFCC

/*
List of touched ROM:
0xC2044C, 0xC21404, 0xC2FFCC (or defined above), 0xC2026E, 0xC20295
0xC2DD9A, 0xC1DDCE, 0xC1DDD5, 0xC2617D, 0xC04A83, 0xC12CAB, 0xC229FF

free_ram:
0 (0x00) -> Initialize sprites.
Bit 0 set -> Raise character 1.
Bit 1 set -> Raise character 2.
Bit 2 set -> Raise character 4.
Bit 3 set -> Raise character 3.
16 (0x10) -> No selected character.
Bit 5 set -> "YOU WON" (set bits to raise alive members).
*/

// Handy macro.
command branch_if_flag_set(flag_code,branch) {
    LDA_i (flag_code)
    JSL (0xC21628)
    CMP_i (1)
    BEQ_a (branch)
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// This makes it so windows and odometers don't raise in battle.
// Cheap hijack, this uses 5 bytes of free ROM starting from C2/FFCC (or defined above).
ROM[0xC2044C] = JSR (FREE_ROM_C2_S)
ROM[0xC21404] = JSR (FREE_ROM_C2_S)

ROM[FREE_ROM_C2_L] = {
    JSL (handle_windows)    // C2/FFCC
    RTS                        // C2/FFD0
}

handle_windows: {
    LDA_a (0x9643)
    CMP_i (1)
    BEQ (4)
    LDA_i (0x0012)
    RTL
    LDA_i (0x0013)
    RTL
}

// We change the position of the AUTO indicator for it to not cover the sprite in position x6_pos.
ROM[0xC2026E] = LDY_i (0x80B2)
ROM[0xC20295] = LDY_i (0x80B2)

// We remove the blinking effect when a member is hit.
ROM[0xC2DD9A] = {	// Faster that 4 NOP's.
    BRA(0)
    BRA(0)
}

// We hijack the JSL's at Battle_RaiseHPBox and Battle_LowerHPBox to call our new functions (this is necessary due to the way I'm controlling the action scripts).
ROM[0xC1DDCE] = JSL (new_raise_routine)
ROM[0xC1DDD5] = JSL (new_lower_routine)

new_raise_routine: {
    JSL (0xC43573)                            // RaiseHPBox, we still gotta call this one.
    LDA_a (free_ram)
    BNE (4)
    JSL (m3sbs_init)
    LDA_a (RAM_SELECTED_CHARACTER)
    CMP_i (2)
    BNE (7)
    LDA_i (8)
    STA_a (free_ram)
    RTL
    LDA_a (RAM_SELECTED_CHARACTER)
    CLC
    ADC_i (1)
    STA_a (free_ram)
    RTL
}

new_lower_routine: {
    LDA_a (free_ram)
    BEQ (6)
    LDA_i (16)
    STA_a (free_ram)
    JSL (0xC3E6F8)                            // LowerHPBox, we still gotta call this one.
    RTL
}

// We do this hijack to ensure that Bowspr finishes once you win, die or run away from a battle.
ROM[0xC2617D] = JSL (end_bowspr_hj)
end_bowspr_hj: {
    JSL (0xC1DD5F)                            // Original JSL.
    JSL (ASM_BowsprEnd)
    JSL (0xC216DB) // Update Teddy bears.
    JSL (m3sbs_reset)
    RTL
}

// We do this hijack to reset RAM right before the battle.
ROM[0xC04A83] = JSL (before_battle_hj)
before_battle_hj: {
    JSL (m3sbs_reset)
    JSL (0xC0D19B)                            // Original JSL.
    RTL
}

// We do this hijack to set RAM properly for raising the alive party members' sprites during the "you won" screen.
ROM[0xC12CAB] = JSL (you_won_hj)
you_won_hj: {
    JSL (you_won_ram_set)
    JSL (0xC12DD5)                            // Original JSL.
    RTL
}

you_won_ram_set: {
    LDA_a (free_ram)
    ORA_i (32)
    STA_a (free_ram)
    LDA_a (RAM_BS_STATUS_0)
    CMP_i (0x0001)
    BEQ (17)
    LDA_a (RAM_BS_STATUS_0)
    CMP_i (0x0002)
    BEQ (9)
    LDA_a (free_ram)
    ORA_i (1)
    STA_a (free_ram)

    LDA_a (RAM_BS_STATUS_1)
    CMP_i (0x0001)
    BEQ (17)
    LDA_a (RAM_BS_STATUS_1)
    CMP_i (0x0002)
    BEQ (9)
    LDA_a (free_ram)
    ORA_i (2)
    STA_a (free_ram)

    LDA_a (RAM_BS_STATUS_2)
    CMP_i (0x0001)
    BEQ (17)
    LDA_a (RAM_BS_STATUS_2)
    CMP_i (0x0002)
    BEQ (9)
    LDA_a (free_ram)
    ORA_i (8)
    STA_a (free_ram)

    LDA_a (RAM_BS_STATUS_3)
    CMP_i (0x0001)
    BEQ (17)
    LDA_a (RAM_BS_STATUS_3)
    CMP_i (0x0002)
    BEQ (9)
    LDA_a (free_ram)
    ORA_i (4)
    STA_a (free_ram)
    RTL
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Action scripts for each character. We need four so that each one uses a different result register.
m3sbs_0: {
    m_set_var2(1)
    m_jsl(m3sbs_main)
}

m3sbs_1: {
    m_set_var2(2)
    m_jsl(m3sbs_main)
}

m3sbs_2: {
    m_set_var2(8)
    m_jsl(m3sbs_main)
}

m3sbs_3: {
    m_set_var2(4)
    m_jsl(m3sbs_main)
}

// Common action script subroutine.
m3sbs_main: {
    m_get_mem16(RAM_BGMODE_MIRROR)
    m_and_result(1)
    m_jeq(p_mode0)

p_mode1:
    m_onposition(0xA055)
    m_jmp(d_continue)
p_mode0:
    m_onposition(0xA023)

d_continue:
    m_get_var1
    
    m_multijmp(7)
	short use_x0
	short use_x1
	short use_x2
	short use_x3
	short use_x4
	short use_x5
	short use_x6
        
use_x0:
    m_set_xpos(x0_pos)
    m_jmp(d_end)
use_x1:
    m_set_xpos(x1_pos)
    m_jmp(d_end)
use_x2:
    m_set_xpos(x2_pos)
    m_jmp(d_end)
use_x3:
    m_set_xpos(x3_pos)
    m_jmp(d_end)
use_x4:
    m_set_xpos(x4_pos)
    m_jmp(d_end)
use_x5:
    m_set_xpos(x5_pos)
    m_jmp(d_end)
use_x6:
    m_set_xpos(x6_pos)
    m_jmp(d_end)
    
d_end:
    m_priority(0)
    m_set_var0(1)
    m_set_facing_anim(direction,anim_frame)
    m_set_anim(-1)
    
lower:
    m_set_yvel(512)
    m_pause(8)
    m_set_yvel(0)
    m_set_anim(-1)

loop1: 
    m_pause(1)
    m_asmcall(var2_and_free_ram)
    m_jne(raise)
    m_jmp(loop1)

raise: 
    m_set_anim(anim_frame)
    m_set_ypos(y_pos)
    m_set_yvel(-512)
    m_pause(4)

    m_loop(4)
	m_pause(1)
	m_asmcall(var2_and_free_ram)
	m_jeq(lower)
    m_endloop

    m_set_yvel(0)

loop2: 
    m_pause(1)
    m_get_mem16(free_ram)
    m_and_result(32)
    m_jne(you_won)
    m_asmcall(var2_and_free_ram)
    m_jeq(lower)
    m_jmp(loop2)

you_won:
    m_set_facing_anim(yw_direction,yw_anim_frame)
    m_halt
}

// ASM subroutine for SCR_RESULT = VAR2 && free_ram.
var2_and_free_ram: {
    LDX_d (0x88)
    LDA_x (RAM_OBJ_SCRIPT_VAR2)
    AND_a (free_ram)
    RTL
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Function to create the Bowspr sprites.
m3sbs_create: {
    PHY

    JSL (m3sbs_sprite_detect)
    BNE_a (c_continue) PLY RTL

c_continue:
    JSL (ASM_BowsprCreateEntity)
    TYX
    
    PLY
    
    // A: Variable - X: Object Index. - Y: x_pos to use (0-6).
    
    // Set object var 1 to x_pos to use.
    TYA
    STA_x (RAM_OBJ_SCRIPT_VAR1)
    
    // Increase x_pos by 2 for the next sprite.
    INY INY
    RTL
}

// Subroutine to detect the sprite to use based on event flags.
m3sbs_sprite_detect: {
    TXA
    CMP_i (1)
    BEQ_a (mc1)
    
    TXA
    CMP_i (2)
    BEQ_a (mc2)
    
    TXA
    CMP_i (3)
    BEQ_a (mc3)

    TXA
    CMP_i (4)
    BEQ_a (mc4)
    
    LDA_i (0)
    BRA_a (d_end)

mc1:
    LDA_a(0xA97A)
    BNE_a(mc1_use_robot_sprite)

    LDA_a(0x9887)
    CMP_i(4)
    BEQ_a(mc1_use_magicant_sprite)
    CMP_i(5)
    BEQ_a(mc1_use_robot_sprite)
    // Use normal sprite.
    LDA_i (mc1_sprite)
    BRA_a (d_end)

mc1_use_magicant_sprite:
    LDA_i (mc1_altsprite1)
    BRA_a (d_end)

mc1_use_robot_sprite:
    LDA_i (mc1_altsprite2)
    BRA_a (d_end)
    
mc2:
    LDA_a(0xA97A)
    BNE_a(mc2_use_robot_sprite)

    LDA_a(0x9887)
    CMP_i(5)
    BEQ_a(mc2_use_robot_sprite)
    // Use normal sprite.
    LDA_i (mc2_sprite)
    BRA_a (d_end)

mc2_use_robot_sprite:
    LDA_i (mc2_altsprite1)
    BRA_a (d_end)

mc3:
    LDA_a(0xA97A)
    BNE_a(mc3_use_robot_sprite)

    LDA_a(0x9887)
    CMP_i(5)
    BEQ_a(mc3_use_robot_sprite)
    // Use normal sprite.
    LDA_i (mc3_sprite)
    BRA_a (d_end)

mc3_use_robot_sprite:
    LDA_i (mc3_altsprite1)
    BRA_a (d_end)
    
mc4:
    LDA_a(0xA97A)
    BNE_a(mc4_use_robot_sprite)

    LDA_a(0x9887)
    CMP_i(5)
    BEQ_a(mc4_use_robot_sprite)
    // Use normal sprite.
    LDA_i (mc4_sprite)
    BRA_a (d_end)

mc4_use_robot_sprite:
    LDA_i (mc4_altsprite1)
    BRA_a (d_end)
    
d_end:
    RTL
}

// This function creates the correct sprites in the right places based on the party member configuration.
m3sbs_init: {
    JSL (ASM_BowsprEnd)            // End Bowspr first.

    // Check if current battle group is GIYGAS_5 + POKEY (normally 478).
    LDA_a (0x4A8C)
    CMP_i (giygas_special_group)
    BNE (5)
    LDA_i (2)                    // Use free VRAM in the area reserved for battle sprites (for Giygas).
    BRA (3)
    LDA_i (1)                    // Use free VRAM in the area reserved for the second battle BG.
    
    LDX_i (bowspr_palette)        // Palette setting.
    JSL (ASM_BowsprBegin)

    // Load the correct value into Y, which will be used as the x_pos for the first character.
    LDA_a (RAM_CONTROLLED_MEMBERS)
    CMP_i (1)
    BNE (6)
    LDY_i (3)
    BRA_a (i_start)
    LDA_a (RAM_CONTROLLED_MEMBERS)
    CMP_i (2)
    BNE (6)
    LDY_i (2)
    BRA_a (i_start)
    LDA_a (RAM_CONTROLLED_MEMBERS)
    CMP_i (3)
    BNE (6)
    LDY_i (1)
    BRA_a (i_start)
    LDY_i (0)
    
i_start:
    LDX_a (RAM_BS_CHAR_0)
    ASMLoadAddress0E (m3sbs_0)
    JSL (m3sbs_create)
    LDX_a (RAM_BS_CHAR_1)
    ASMLoadAddress0E (m3sbs_1)
    JSL (m3sbs_create)
    LDX_a (RAM_BS_CHAR_2)
    ASMLoadAddress0E (m3sbs_2)
    JSL (m3sbs_create)
    LDX_a (RAM_BS_CHAR_3)
    ASMLoadAddress0E (m3sbs_3)
    JSL (m3sbs_create)
    
    RTL
}
