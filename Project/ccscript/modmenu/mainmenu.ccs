import keyitems
//import "../modmenu/modmenu.ccs"
//import "../modmenu/config/configmenu.ccs"
/* ASM */
import "../essential/asm65816.ccs"
import "../essential/ccscript_missing_commands.ccs"
import "../essential/flags.ccs"
import "../redux/window_titles.ccs"
import "../modmenu/cc_main_menu.ccs"
import "../modmenu/keyitems.ccs"

// Define window constants
define MAINMENU_WINDOW	= 0
define DIALOGUE_WINDOW	= 1
define WHICH_WINDOW	= 40
define FULL_WINDOW	= 61
define KEYITEMS_WINDOW	= 62
define USE_HELP_WINDOW	= 63
define PSI_MAIN_WINDOW	= 64

// Definitions for each Key item:
define ATM_CARD		= 177	// 0xB1
define SOUND_STONE	= 196	// 0xC4
define TOWN_MAP		= 202	// 0xCA
define SHACK_KEY	= 170	// 0xAA
define BICYCLE		= 176	// 0xB0
define RECEIVER_PHONE	= 181	// 0xB5
define PENCIL_ERASER	= 184	// 0xB8
define ERASER_ERASER	= 210	// 0xD2
define CABIN_KEY	= 171	// 0xAB
define WAD_BILLS	= 180	// 0xB4
define BACKSTAGE_PASS	= 125	// 0x7D
define SHOW_TICKET	= 178	// 0xB2
define ZOMBIE_PAPER	= 174	// 0xAE
define DIAMOND		= 182	// 0xB6
define YOGURT_DISP	= 139	// 0x8B
define SIGNED_BANANA	= 183	// 0xB7
define HIEROGLYPH	= 185	// 0xB9
define CARROT_KEY	= 253	// 0xFD
define TOWER_KEY	= 192	// 0xC0
define HAWK_EYE		= 175	// 0xAF
define SHYNESS_BOOK	= 164	// 0xA4
define TENDAKRAUT	= 211	// 0xD3
define METEORITE	= 193	// 0xC1
define LOCKER_KEY	= 205	// 0xCD
define BAD_KEY_MACHINE	= 172	// 0xAC
define BUBBLE_GUM	= 104	// 0x68

// Open custom menu instead of regular one, then forcibly close the text + HP windows
/*ROM[0xC134AF] = {
	MText	(MainMenu)
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	JSR	(HIDE_HPPP_WINDOWS)
	JMP	(0xC13C8C)	// Close window?
}*/

Selected_Goods:		{ counter(1) eob }
Selected_Equip:		{ counter(2) eob }
Selected_PSI:		{ counter(3) eob }
Selected_Status:	{ counter(4) eob }
Selected_KeyItems:	{ counter(5) eob }
//Selected_ModMenu:	{ counter(6) eob }

MainMenu:
	//unset(flg_RandomWarp)
	if (flg_EndLoop) { unset(flg_EndLoop) goto(goto_end) }
	//sprite_spawn(106, 896, 1) // Invisible camera sprite running "Cutscene On"
	// Stop player movement
	freeze_char_movement(0xFF)
	// Open appropriately sized window
	window_closeall
	if flag flg_LearnedPSI { window_open(MAINMENU_WINDOW) }
	else { window_open(PSI_MAIN_WINDOW) }
	//window_open(MAINMENU_WINDOW)
	sound(SND_CURSORBLIP)	// Sound effect for opening the menu
	//title(0, "Action[00]")	// Lbl(lbl_Action), Don't show a window title for the Main menu
	counter_zero
	ctoarg
	// Options
	add_menu_option_with_callback("Goods", Selected_Goods)	// MenuOptionWithCallback("Goods", Selected_Goods)
	add_menu_option_with_callback("Equip", Selected_Equip)
	if flag flg_LearnedPSI {
		check_party_char(1)
		if result_is(1) {
			add_menu_option_with_callback("PSI", Selected_PSI) }
		else {
			check_party_char(4)
			if result_is(1) {
				add_menu_option_with_callback("PSI", Selected_PSI) } 
		} 
	}
	//if flg_ModMenu { add_menu_option_with_callback("Mod Menu", Selected_ModMenu) }
	//if not flg_ModMenu { add_menu_option_with_callback("Config", Selected_ModMenu) }
	add_menu_option_with_callback("Status", Selected_Status)
	//if flg_KeyItems { add_menu_option_with_callback("Key Items", Selected_KeyItems) }
	add_menu_option_with_callback("Key Items", Selected_KeyItems)
	// Width
	print_vertical(2)
	// Exit by default
	if not create_menu_cancellable { goto(goto_end) }
	// Results
	ctoarg
	swap
	switch_goto(5, {
	        long goto_goods
		long goto_equip
		long goto_psi
		long goto_status
		long goto_keyitems
		//long goto_modmenu
	})

	swap
	"@Error! No option {number(0)} in this menu." goto(goto_end)
	goto_goods:
		window_closeall open_hp open_goods goto(MainMenu)
	goto_equip:
		window_closeall open_equip goto(MainMenu)
	goto_psi:
		window_closeall open_hp open_psi goto(MainMenu)
	goto_status:
		window_closeall open_status goto(MainMenu)
	goto_keyitems:
		window_closeall call(keyitems.KeyItems) goto(MainMenu)
	//goto_modmenu:
		//window_closeall if flg_ModMenu { call(modmenu.ModMenu) } if not flg_ModMenu { call(configmenu.ConfigMenu) } goto(goto_end)
		//window_closeall  goto(goto_end)
	goto_end:
		window_closeall
		if flg_Proceed { unset(flg_Proceed) goto(MainMenu) } // Loop Main Menu while flag is on
        //sprite_spawn(106, 897, 1) // Invisible camera sprite running "Cutscene Off"
        // Resume player movement
        reset_camera
eob

//------------------------------------
//		Commands
//------------------------------------
// Check if you currently have a specific party member
command check_party_char(charaID)
{
    counter(1)
_loop:
    ctoarg
    party_pos_check(0)
    if result_is(charaID)
    {
        party_size_check(9)
        goto(_loopend) // Return result = 1
    }
    inc
    // Make sure we only check up to 6 party members lots
    if counter_is(7) { // Is counter >= 7?
        party_size_check(1) // Set result to 0
        goto(_loopend)
    }
    goto(_loop)
_loopend:
}

// Set the flag for the corresponding Key Item
command give_menu_key_item(itemID)
{
	counter(itemID)
	call(keyitemcounter)

	sound(SND_SPECIALITEM)
	"@(The {itemname(itemID)} was added to your Key Items.)" next
}

keyitemcounter:
	if counter_is(SOUND_STONE) { toggle_with_sound(FLG_BUNBUN) }
	if counter_is(SHACK_KEY) { toggle_with_sound(FLG_KEY_TABIGOYA) }
	if counter_is(BICYCLE) { toggle_with_sound(FLG_ITEM_CYCLE) }
	if counter_is(TOWN_MAP) { toggle_with_sound(FLG_ITEM_MAP) }
	if counter_is(RECEIVER_PHONE) { toggle_with_sound(FLG_ITEM_PHONE) }
	if counter_is(PENCIL_ERASER) { toggle_with_sound(FLG_ITEM_TACO_ERASER) }
	if counter_is(CABIN_KEY) { toggle_with_sound(FLG_HAPPY_CARPAINTER_ITEMFULL) }
	if counter_is(WAD_BILLS) { toggle_with_sound(FLG_TWSN_ITEM_TONCHIKI) }
	if counter_is(ZOMBIE_PAPER) { toggle_with_sound(FLG_ITEM_HOIHOI) }
	if counter_is(LOCKER_KEY) { toggle_with_sound(FLG_ITEM_KEY_LOCKER) }
	if counter_is(BAD_KEY_MACHINE) { toggle_with_sound(FLG_ITEM_GAUS) }
	if counter_is(ERASER_ERASER) { toggle_with_sound(FLG_ITEM_KOKESHI) }
	if counter_is(DIAMOND) { toggle_with_sound(FLG_DSRT_ITEM_DIA) }
	if counter_is(SIGNED_BANANA) { toggle_with_sound(FLG_ITEM_BANANA) }
	if counter_is(HIEROGLYPH) { toggle_with_sound(FLG_SUMS_HIEROGLYPH) }
	if counter_is(CARROT_KEY) { toggle_with_sound(flg_KeyItem_CarrotKey) }
	if counter_is(TOWER_KEY) { toggle_with_sound(FLG_ITEM_KEY_PUPUKA) }
	if counter_is(HAWK_EYE) { toggle_with_sound(FLG_ITEM_TAKANOME) }
	if counter_is(SHYNESS_BOOK) { toggle_with_sound(FLG_ITEM_MUKUCHI) }
	if counter_is(TENDAKRAUT) { toggle_with_sound(FLG_GUMI_OLDMAN_ITEM) }
	if counter_is(METEORITE) { toggle_with_sound(FLG_ITEM_XYZ) }
	if counter_is(BUBBLE_GUM) { toggle_with_sound(FLG_ITEM_BUBBLE_GUM) }
	if counter_is(BACKSTAGE_PASS) { toggle_with_sound(flg_KeyItem_BackstagePass) }
	if counter_is(YOGURT_DISP) { toggle_with_sound(flg_KeyItem_YogurtMachine) }
	if counter_is(SHOW_TICKET) { toggle_with_sound(flg_KeyItem_ShowTicket) }
eob

// Turn active flag off or vice versa
command toggle_with_sound(flg)
{
	copy_wram
	toggle(flg)
	if flag flg { sound(SND_GIFTOPEN) }
	else { sound(SND_BOXTAKE) }
	copy_active
}

// Set or unset a flag depending on the presence of a certain party member
command set_char_flag_if_inparty(charaID, flg)
{
	check_party_char(charaID)
	set_flag_result(flg)
}

command set_flag_result(flg)
{
	if result_is(1) { set(flg) }
	else { unset(flg) }
}

// Update all party member flags to reflect current party 
command set_party_flags()
{
	call(setptyflags)
}

setptyflags:
	set_char_flag_if_inparty(1, flg_Ness)
	set_char_flag_if_inparty(2, flg_Paula)
	set_char_flag_if_inparty(3, flg_Jeff)
	set_char_flag_if_inparty(4, flg_Poo)
	set_char_flag_if_inparty(5, FLG_POKEY)
	set_char_flag_if_inparty(6, FLG_PICKEY)
	set_char_flag_if_inparty(7, flg_King)
	set_char_flag_if_inparty(8, flg_Tony)
	set_char_flag_if_inparty(9, FLG_BALLOONMONKEY)
	set_char_flag_if_inparty(10, FLG_DUNGEONMAN)
	set_char_flag_if_inparty(11, flg_FlyingMan)
	set_char_flag_if_inparty(16, flg_TeddyBear)
	set_char_flag_if_inparty(17, flg_PlushBear)
	eob
