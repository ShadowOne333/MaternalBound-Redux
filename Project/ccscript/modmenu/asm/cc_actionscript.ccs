import "../../essential/asm65816.ccs"
import "../../essential/ccexpand.ccs"


command cc_set_sprite_script(spr, ptr) "[1A 1B {short spr} {adr24(ptr)}]"
command cc_set_npc_script(spr, ptr)    "[1A 1C {short spr} {adr24(ptr)}]"


command wait_movement               "[1F 61]"
command create_sprite(spr, scr, fx) "[1F 15 {short spr} {short scr} {short fx}]"
command create_sprite_actionscript(spr, ptr, fx) {
    create_sprite(spr, 35, fx)      // 35 is the "instant delete" script. We will overwrite it immediately anyways, it won't run until next frame
    cc_set_sprite_script(spr, ptr)
}

define GetSpriteObjectSlot = 0xC46028 // Get slot of an object that has the specified spritegroup
define GetNpcObjectSlot    = 0xC4605A // Get slot of an object that has the specified NPC ID

_SetScriptPtr: {
            ASL
            TAX
            LDY_x (0x0ADA) // Get script table offset

            LDA_a (D_cc_argv_2)
            STA_y (0x13FE) // Set script program counter
            LDA_a (D_cc_argv_4)
            AND_i (0x00FF)
            STA_y (0x148A) // Set script program bank
            RTS
}

// Set actionscript of sprite to specified pointer
IMPL_SpriteScript:
            LDY_i (0x0005) // spr (2) + ptr (3)
            JSL   (R_Read_Parameter_Bytes)

            LDA_a (D_cc_argv)
            JSL   (GetSpriteObjectSlot)
            CMP_i (0xFFFF)
            BEQ   (3)      // Skip JSR
            JSR   (_SetScriptPtr)
            RTL

// Set actionscript of NPC to specified pointer
IMPL_NpcScript:
            LDY_i (0x0005) // npc (2) + ptr (3)
            JSL   (R_Read_Parameter_Bytes)

            LDA_a (D_cc_argv)
            JSL   (GetNpcObjectSlot)
            CMP_i (0xFFFF)
            BEQ   (3)      // Skip JSR
            JSR   (_SetScriptPtr)
            RTL

